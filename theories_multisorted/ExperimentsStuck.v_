From Prototype Require Import MPrelude MSig.

(*********************************************************************************)
(** *** Level one. *)
(*********************************************************************************)

Module O.
Section WithSig.
Context {sig : signature}.

Lemma occurs_iff s s' : occursb s s' <-> occurs s s'.
Proof. now destruct (occurs_spec s s'). Qed.

Lemma has_var_iff s : has_varb s <-> has_var s.
Proof. now destruct (has_var_spec s). Qed.

Lemma occurs_in {s1 s2 ty} {c : fin (nctor s2)} : 
  occurs_ty s1 ty -> In ty (ctor_type c) -> occurs s1 s2.
Proof.
intros H1 H2. apply occurs_base. exists c. rewrite Exists_exists. eauto.
Qed.

Lemma occurs_aux s s' : occursb s s' -> occurs s s'.
Proof. now destruct (occurs_spec s s'). Qed.

Lemma has_var_aux s : has_varb s -> has_var s.
Proof. now destruct (has_var_spec s). Qed.

Lemma has_var_aux' s : has_var s -> has_varb s.
Proof. now destruct (has_var_spec s). Qed.

(** Level one terms. *)

Inductive term : fin nsort -> Type :=
| T_var {s} : has_varb s -> nat -> term s
| T_ctor {s} (c : fin (nctor s)) : args (ctor_type c) -> term s

with args : list arg_ty -> Type :=
| AL_nil : args []
| AL_cons {ty tys} : arg ty -> args tys -> args (ty :: tys)

with arg : arg_ty -> Type :=
| A_base b : base_type b -> arg (AT_base b)
| A_term {s} : term s -> arg (AT_term s)
| A_bind {s s'} : term s' -> arg (AT_bind s s'). 

Derive Signature NoConfusion NoConfusionHom for term args arg.

(** Size of terms. *)

Equations term_size {s} : term s -> nat :=
term_size (T_var _ _) := 0 ;
term_size (T_ctor c al) := S (args_size al) ;

with args_size {tys} : args tys -> nat :=
args_size AL_nil := 0 ;
args_size (AL_cons a al) := S (arg_size a + args_size al)

with arg_size {ty} : arg ty -> nat :=
arg_size (A_base _ _) := 0 ;
arg_size (A_term t) := S (term_size t) ;
arg_size (A_bind t) := S (term_size t).

(** Well-founded induction on terms based on their size. *)

Section SizeInd.
  Context (P : forall s, term s -> Prop).

  Context (IH : forall s (t : term s), 
    (forall s' (t' : term s'), term_size t' < term_size t -> P s' t') -> 
    P s t).

  Lemma size_ind : forall s t, P s t.
  Proof.
  intros s t. remember (term_size t) as n eqn:E.
  revert s t E. induction n using Wf_nat.lt_wf_ind. intros s t ->.
  apply IH. intros s' t' Hlt. eapply H ; eauto.
  Qed.
End SizeInd.

(** Renamings. *)

Definition ren (s : fin nsort) := nat -> nat.

(** Vector of renamings which can be applied to a term of sort [s]. *)
Definition ren_vec (s : fin nsort) :=
  hvector nsort (fun s' => occursb s' s -> has_varb s' -> ren s').

(** Select a subvector of a vector of renaming. *)
Equations? ren_subvector {s1 s2} (p : occursb s1 s2) (rv : ren_vec s2) : ren_vec s1 := 
ren_subvector p rv := 
  hvector_map 
    (fun s' (r : occursb s' s2 -> has_varb s' -> ren s') pocc pvar => r _ pvar) rv.
Proof. 
rewrite occurs_iff. apply occurs_trans with (s' := s1).
- now rewrite <-occurs_iff.
- now rewrite <-occurs_iff. 
Qed.

(** Apply a vector of renamings to a term. *)
Equations? rename {s} (rv : ren_vec s) (t : term s) : term s :=
rename rv (T_var p i) := T_var p (hvector_nth s rv _ p i) ;
rename rv (T_ctor c al) := T_ctor c (rename_args c _ rv al)

with rename_args {s} {tys : list arg_ty} (c : fin (nctor s)) (p : incl tys (ctor_type c)) 
  (rv : ren_vec s) (al : args tys) : args tys :=
rename_args c p rv AL_nil := AL_nil ;
rename_args c p rv (AL_cons a al) := AL_cons (rename_arg c _ rv a) (rename_args c _ rv al)

with rename_arg {s} {ty : arg_ty} (c : fin (nctor s)) (p : In ty (ctor_type c))
  (rv : ren_vec s) (a : arg ty) : arg ty :=
rename_arg c p rv (A_base b x) := A_base b x ;
rename_arg c p rv (A_term t) := A_term (rename (ren_subvector _ rv) t) ;
rename_arg c p rv (A_bind a) := A_bind a.

Proof.
- rewrite occurs_iff. apply occurs_refl. 
- apply p. now left.
- apply p. now right.
- rewrite occurs_iff. unshelve eapply (occurs_in _ p). constructor.
Qed.

End WithSig.
End O.

(*********************************************************************************)
(** *** Level zero. *)
(*********************************************************************************)

(** Types, terms, and values. *)

Unset Elimination Schemes. 

Inductive ty : Type :=
| var_ty : nat -> ty
| arr : ty -> ty -> ty
| all : ty -> ty.

Inductive tm : Type :=
| app : tm -> tm -> tm
| tapp : tm -> ty -> tm
| vt : vl -> tm

with vl : Type :=
| var_vl : nat -> vl
| lam : ty -> tm -> vl
| tlam : tm -> vl.

Set Elimination Schemes.

Scheme ty_ind := Induction for ty Sort Prop.
Scheme tm_ind := Induction for tm Sort Prop 
  with vl_ind := Induction for vl Sort Prop.
Combined Scheme tm_vl_ind from tm_ind, vl_ind.

(** Renaming. *)

Definition ren := nat -> nat.

Equations rename_ty : ren -> ty -> ty :=
rename_ty rty (var_ty i) := var_ty (rty i) ;
rename_ty rty (arr t1 t2) := arr (rename_ty rty t1) (rename_ty rty t2) ;
rename_ty rty (all t) := all (rename_ty (up_ren rty) t).

Equations rename_tm : ren * ren -> tm -> tm :=
rename_tm (rty, rvl) (app t1 t2) := app (rename_tm (rty, rvl) t1) (rename_tm (rty, rvl) t2) ;
rename_tm (rty, rvl) (tapp t1 t2) := tapp (rename_tm (rty, rvl) t1) (rename_ty rty t2) ;
rename_tm (rty, rvl) (vt v) := vt (rename_vl (rty, rvl) v)

with rename_vl : ren * ren -> vl -> vl :=
rename_vl (rty, rvl) (var_vl i) := var_vl (rvl i) ;
rename_vl (rty, rvl) (lam t1 t2) := lam (rename_ty rty t1) (rename_tm (rty, up_ren rvl) t2) ;
rename_vl (rty, rvl) (tlam t) := tlam (rename_tm (up_ren rty, rvl) t).

(*********************************************************************************)
(** *** Signature. *)
(*********************************************************************************)

Definition nbase_ := 0.

Definition base_type_ (b : fin nbase_) : Type :=
  vector_nth b [- -].

Definition nsort_ := 3.
Notation sty := (finO) (only parsing).
Notation stm := (finS finO) (only parsing).
Notation svl := (finS (finS finO)) (only parsing).

Definition nctor_ (s : fin nsort_) : nat :=
  vector_nth s [- 2 ; 3 ; 2 -].

Definition ctor_type_hvector : @hvector nsort_ (fun s => vector (list (@arg_ty nbase_ nsort_)) (nctor_ s)) :=
  let x := [- [ AT_term sty ; AT_term sty ] ; [ AT_bind sty sty ] -]%vector in
  let y := [- [ AT_term stm ; AT_term stm ] ; [ AT_term stm ; AT_term sty ] ; [ AT_term svl ] -]%vector in
  let z := [- [ AT_term sty ; AT_bind svl stm ] ; [ AT_bind sty stm ] -]%vector in
  [= x ; y ; z =].
  
Definition ctor_type_ (s : fin nsort_) (c : fin (nctor_ s)) : list (@arg_ty nbase_ nsort_) :=
  vector_nth c (hvector_nth s ctor_type_hvector).

Definition sig : signature := 
  Build_signature nbase_ base_type_ nsort_ nctor_ ctor_type_.

(*********************************************************************************)
(** *** Reification. *)
(*********************************************************************************)

Fixpoint reify_ty (t : ty) : @O.term sig sty :=
  match t with 
  | var_ty i => O.T_var (@eq_refl bool (@has_varb sig sty)) i
  | arr t1 t2 => 
      @O.T_ctor sig sty finO (O.AL_cons (O.A_term (reify_ty t1)) (O.AL_cons (O.A_term (reify_ty t2)) O.AL_nil))
  | all t => 
      @O.T_ctor sig sty (finS finO) (O.AL_cons (O.A_bind (reify_ty t)) O.AL_nil)
  end.

Fixpoint reify_tm (t : tm) : @O.term sig stm :=
  match t with 
  | app t1 t2 =>
      @O.T_ctor sig stm finO (O.AL_cons (O.A_term (reify_tm t1)) (O.AL_cons (O.A_term (reify_tm t2)) O.AL_nil))
  | tapp t1 t2 =>
      @O.T_ctor sig stm (finS finO) (O.AL_cons (O.A_term (reify_tm t1)) (O.AL_cons (O.A_term (reify_ty t2)) O.AL_nil))
  | vt t =>
      @O.T_ctor sig stm (finS (finS finO)) (O.AL_cons (O.A_term (reify_vl t)) O.AL_nil)
  end

with reify_vl (t : vl) : @O.term sig svl :=
  match t with 
  | var_vl i => O.T_var (@eq_refl bool (@has_varb sig svl)) i
  | lam t1 t2 =>
      @O.T_ctor sig svl finO (O.AL_cons (O.A_term (reify_ty t1)) (O.AL_cons (O.A_bind (reify_tm t2)) O.AL_nil))
  | tlam t =>
      @O.T_ctor sig svl (finS finO) (O.AL_cons (O.A_bind (reify_tm t)) O.AL_nil)
  end.

(*********************************************************************************)
(** *** Evaluation. *)
(*********************************************************************************)

Lemma explode {A : Type} : is_true false -> A.
Proof. intros H. unfold is_true in H. discriminate. Qed.

Definition eval_sort (s : fin (@nsort sig)) : Type :=
  vector_nth s [- ty ; tm ; vl -].

Definition eval_arg_ty (ty : @arg_ty (@nbase sig) (@nsort sig)) : Type :=
  match ty with 
  | AT_base b => @base_type sig b 
  | AT_term s => eval_sort s
  | AT_bind _ s => eval_sort s
  end.

Fixpoint eval_arg_tys (tys : list (@arg_ty (@nbase sig) (@nsort sig))) : Type :=
  match tys with 
  | [] => unit 
  | ty :: tys => eval_arg_ty ty * eval_arg_tys tys 
  end.

Definition eval_ctor_sty (c : fin (@nctor sig sty)) : eval_arg_tys (ctor_type c) -> eval_sort sty :=
  let x '(t1, (t2, tt)) := arr t1 t2 in
  let y '(t, tt) := all t in 
  hvector_nth (T := fun c => eval_arg_tys (ctor_type c) -> eval_sort sty) c [= x ; y =].

Definition eval_ctor_stm (c : fin (@nctor sig stm)) : eval_arg_tys (ctor_type c) -> eval_sort stm :=
  let x '(t1, (t2, tt)) := app t1 t2 in
  let y '(t1, (t2, tt)) := tapp t1 t2 in
  let z '(t, tt) := vt t in
  hvector_nth (T := fun c => eval_arg_tys (ctor_type c) -> eval_sort stm) c [= x ; y ; z =].

Definition eval_ctor_svl (c : fin (@nctor sig svl)) : eval_arg_tys (ctor_type c) -> eval_sort svl :=
  let x '(t1, (t2, tt)) := lam t1 t2 in
  let y '(t, tt) := tlam t in
  hvector_nth (T := fun c => eval_arg_tys (ctor_type c) -> eval_sort svl) c [= x ; y =].

Definition eval_ctor (s : fin (@nsort sig)) (c : fin (nctor s)) 
  (args : eval_arg_tys (ctor_type c)) : eval_sort s :=
  hvector_nth s (T := fun s => forall c : fin (@nctor sig s), eval_arg_tys (ctor_type c) -> eval_sort s) 
    [= eval_ctor_sty ; eval_ctor_stm ; eval_ctor_svl =] c args.

Definition eval_var {s} (p : has_varb s) (i : nat) : eval_sort s :=
  let x _ := var_ty i in
  let z _ := var_vl i in
  hvector_nth (T := fun s => has_varb s -> eval_sort s) s [= x ; explode ; z =] p.

Fixpoint eval {s} (t : @O.term sig s) : eval_sort s :=
  match t with 
  | O.T_var p i => eval_var p i
  | O.T_ctor c al => eval_ctor _ c (eval_args al)
  end 

with eval_args {tys} (al : O.args tys) : eval_arg_tys tys :=
  match al with 
  | O.AL_nil => tt
  | O.AL_cons a al => (eval_arg a, eval_args al)
  end 

with eval_arg {ty} (a : O.arg ty) : eval_arg_ty ty :=
  match a with 
  | O.A_base b x => x 
  | O.A_term t => eval t 
  | O.A_bind t => eval t
  end.

(*********************************************************************************)
(** *** Proof of bijection. *)
(*********************************************************************************)

Lemma eval_reify_ty (t : ty) : eval (reify_ty t) = t.
Proof. induction t. all: cbn ; f_equal ; auto. Qed.

Lemma eval_reify_tm : 
  (forall t, eval (reify_tm t) = t) /\ (forall v, eval (reify_vl v) = v).
Proof. apply tm_vl_ind. all: intros ; cbn ; f_equal ; auto using eval_reify_ty. Qed.

(*********************************************************************************)
(** *** Custom induction principle on level one terms. *)
(*********************************************************************************)

Section CustomInd.
  #[local] Existing Instance sig.

  Context (P : forall s, O.term s -> Prop).
  
  Context (Hvar_ty : forall i (p : has_varb sty), P sty (O.T_var p i)).
  Context (Harr : forall t1 t2, P sty t1 -> P sty t2 -> 
    P sty (@O.T_ctor _ sty finO (O.AL_cons (O.A_term t1) (O.AL_cons (O.A_term t2) O.AL_nil)))).
  Context (Hall : forall t, P sty t -> 
    P sty (@O.T_ctor _ sty (finS finO) (O.AL_cons (O.A_bind t) O.AL_nil))).

  Context (Happ : forall t1 t2, P stm t1 -> P stm t2 ->
    P stm (@O.T_ctor _ stm finO (O.AL_cons (O.A_term t1) (O.AL_cons (O.A_term t2) O.AL_nil)))).
  Context (Htapp : forall t1 t2, P stm t1 -> P sty t2 ->
    P stm (@O.T_ctor _ stm (finS finO) (O.AL_cons (O.A_term t1) (O.AL_cons (O.A_term t2) O.AL_nil)))).
  Context (Hvt : forall t, P svl t ->
    P stm (@O.T_ctor _ stm (finS (finS finO)) (O.AL_cons (O.A_term t) O.AL_nil))).

  Context (Hvar_vl : forall i (p : has_varb svl), P svl (O.T_var p i)).
  Context (Hlam : forall t1 t2, P sty t1 -> P stm t2 ->
    P svl (@O.T_ctor _ svl finO (O.AL_cons (O.A_term t1) (O.AL_cons (O.A_bind t2) O.AL_nil)))).
  Context (Htlam : forall t1, P stm t1 ->
    P svl (@O.T_ctor _ svl (finS finO) (O.AL_cons (O.A_bind t1) O.AL_nil))).
  
  Lemma custom_ind {s} : forall t : O.term s, P s t.
  Proof.
  revert s. apply O.size_ind. intros s t IH. (repeat depelim s) ; depelim t.
  - apply Hvar_ty.
  - repeat depelim c.
    + depelim a. depelim a1. depelim a2. depelim a. depelim a1. 
      apply Harr ; apply IH ; cbn ; lia.
    + depelim a. depelim a1. depelim a. 
      apply Hall ; apply IH ; cbn ; lia.
  - exfalso. clear -i. now cbv in i.
  - repeat depelim c.
    + depelim a. depelim a. depelim a1. depelim a. depelim a1. 
      apply Happ ; apply IH ; cbn ; lia.
    + depelim a. depelim a. depelim a1. depelim a. depelim a1. 
      apply Htapp ; apply IH ; cbn ; lia.
    + depelim a. depelim a. depelim a1.
      apply Hvt ; apply IH ; cbn ; lia.
  - apply Hvar_vl.
  - repeat depelim c.
    + depelim a. depelim a. depelim a1. depelim a. depelim a1. 
      apply Hlam ; apply IH ; cbn ; lia.
    + depelim a. depelim a. depelim a1.
      apply Htlam ; apply IH ; cbn ; lia.
  Qed. 

End CustomInd.

(*********************************************************************************)
(** *** Transporting renamings. *)
(*********************************************************************************)
  
Definition eval_ren_vec_type (s : fin (@nsort sig)) : Type :=
  vector_nth s [- ren ; (ren * ren)%type ; (ren * ren)%type -].

Definition eval_ren_vec {s : fin (@nsort sig)} (rv : O.ren_vec s) : eval_ren_vec_type s :=
  let x (rv : @O.ren_vec sig sty) :=
    hvector_nth sty rv eq_refl eq_refl
  in
  let y (rv : @O.ren_vec sig stm) :=
    ( hvector_nth sty rv eq_refl eq_refl
    , hvector_nth svl rv eq_refl eq_refl)
  in
  let z (rv : @O.ren_vec sig svl) :=
    ( hvector_nth sty rv eq_refl eq_refl
    , hvector_nth svl rv eq_refl eq_refl )
  in
  hvector_nth (T := fun s => @O.ren_vec sig s -> eval_ren_vec_type s) s [= x ; y ; z =] rv.

(** Evaluate [O.rename s] into a concrete function such as [rename_ty] or [rename_tm]. *)
Definition eval_rename {s} : eval_ren_vec_type s -> eval_sort s -> eval_sort s :=
  hvector_nth (T := fun s => eval_ren_vec_type s -> eval_sort s -> eval_sort s) 
    s [= rename_ty ; rename_tm ; rename_vl =].

Existing Instance sig.

Ltac red_hvector_nth :=
  repeat match goal with 
  | |- context [ hvector_nth finO (hcons ?x ?xs) ] => 
    change (hvector_nth finO (hcons x xs)) with x
  | |- context [ hvector_nth (finS ?i) (hcons ?x ?xs) ] =>
    change (hvector_nth (finS i) (hcons x xs)) with (hvector_nth i xs)
  end ;
  cbn beta zeta iota.

Lemma rename_ty_congr (r1 r2 : ren) (t1 t2 : ty) :
  r1 =â‚ r2 -> t1 = t2 -> rename_ty r1 t1 = rename_ty r2 t2.
Proof.
intros H <-. induction t1 in r1, r2, H |- * ; simp rename_ty. 
- now rewrite H.
- f_equal ; auto.
- f_equal. apply IHt1. intros [|i] ; [reflexivity|]. cbv. now rewrite H.
Qed.

(** Push [eval] through renamings. *)
Lemma push_eval_rename {s} (rv : O.ren_vec s) (t : O.term s) : 
  eval (O.rename rv t) = eval_rename (eval_ren_vec rv) (eval t).
Proof.
revert rv. pattern t. revert s t. apply custom_ind.
all: intros.
- repeat depelim rv. cbn. f_equal. f_equal ; apply uip.
- repeat depelim rv. simpl. unfold eval_rename. red_hvector_nth.
  simp rename. cbn [eval eval_args eval_arg].
  unfold eval_ctor. red_hvector_nth.
  unfold eval_ctor_sty. red_hvector_nth.
  simpl. rewrite H, H0. f_equal.
  + unfold eval_rename. red_hvector_nth. apply rename_ty_congr ; [|reflexivity].
    intros i. 
Admitted.
    
